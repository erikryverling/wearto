buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha11'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.70'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'se.yverling.wearto'
        versionCode 271000201 // Target SDK, version, release number, multi-apk number
        versionName '1.0.0'

        targetSdkVersion 28
        minSdkVersion 23

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GradleCompatible'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext {
    daggerVersion = '2.16'
    archVerion = '1.1.1'
    androidTestVersion = '1.0.1'
}


dependencies {
    // -- Android dependencies

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.61'

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    kapt "android.arch.lifecycle:compiler:$archVerion"
    implementation "android.arch.lifecycle:runtime:$archVerion"
    implementation "android.arch.lifecycle:extensions:$archVerion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    kapt "android.arch.persistence.room:compiler:$archVerion"
    implementation "android.arch.persistence.room:runtime:$archVerion"
    implementation "android.arch.persistence.room:rxjava2:$archVerion"

    implementation 'com.google.android.support:wearable:2.3.0'
    // TODO Can't bump
    implementation 'com.android.support:wear:27.0.2'
    implementation 'com.google.android.gms:play-services-wearable:15.0.1'

    // -- Third party dependencies

    implementation 'org.jetbrains.anko:anko-commons:0.10.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    // -- Test dependencies

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:2.19.0"
    testImplementation "android.arch.core:core-testing:$archVerion"
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'

    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation 'org.mockito:mockito-android:2.19.0'

    // TODO Can't bump the following:

    androidTestImplementation "com.android.support.test:runner:$androidTestVersion"
    androidTestImplementation "com.android.support.test:rules:$androidTestVersion"

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}