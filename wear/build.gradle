buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.21'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'se.yverling.wearto'
        versionCode 281200001 // Target SDK, version, release number, multi-apk number
        versionName '1.2.0'

        targetSdkVersion 30
        minSdkVersion 23

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GradleCompatible'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext {
    daggerVersion = '2.30.1'
    lifecycleVersion = '2.2.0'
    roomVersion = '2.2.6'
    androidTestVersion = '1.3.0'
}


dependencies {

    // -- Android dependencies

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21'

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    implementation 'com.google.android.support:wearable:2.8.1'
    implementation 'androidx.wear:wear:1.1.0'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'


    // -- Third party dependencies

    implementation 'org.jetbrains.anko:anko-commons:0.10.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'


    // -- Test dependencies

    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-inline:3.6.28"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    androidTestImplementation 'junit:junit:4.13.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'org.mockito:mockito-android:2.22.0'

    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}