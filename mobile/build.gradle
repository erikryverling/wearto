buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
        classpath 'com.google.gms:google-services:4.3.2'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'se.yverling.wearto'
        versionCode 291160000 // Target SDK, version, release number, multi-apk number
        versionName '1.1.6'

        targetSdkVersion 29
        minSdkVersion 23

        testInstrumentationRunner 'se.yverling.wearto.test.WearToTestRunner'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        all {
            buildConfigField 'String', 'API_BASE_URL', "\"https://todoist.com/API/v8/\""
            buildConfigField 'String', 'LICENCES_URL', "\"https://github.com/erikryverling/wearto/blob/master/README.md#license-and-acknowledgments\""
            buildConfigField 'String', 'PRIVACY_POLICY_URL', "\"https://github.com/erikryverling/wearto/blob/master/privacy-policy.md\""
        }

        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [usesCleartextTraffic: "true"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [usesCleartextTraffic: "false"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext {
    daggerVersion = '2.25.2'
    lifecycleVersion = '2.1.0'
    roomVersion = '2.2.1'
    ankoVersion = '0.10.2'
    androidTestVersion = '1.2.0'
    espressoVersion = '3.2.0'
}

dependencies {

    // -- Android dependencies

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50'

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    implementation 'com.google.android.material:material:1.0.0'

    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    implementation 'com.google.android.gms:play-services-wearable:17.0.0'

    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-perf:19.0.1'

    // -- Third party dependencies

    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'

    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    implementation 'uk.co.samuelwall:material-tap-target-prompt:2.5.0'


    // -- Test dependencies

    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.25.2'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:3.0.0"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'

    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    androidTestImplementation 'org.mockito:mockito-android:2.22.0'

    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    //noinspection GradleDependency
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'
}

// NOTE! Firebase Analytics requires a google-services.json file not provided. Thus disabling the Firebase plugins.
//apply plugin: 'com.google.gms.google-services'
//apply plugin: 'com.google.firebase.firebase-perf'