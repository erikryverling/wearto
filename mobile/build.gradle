buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-rc02'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41'
        classpath 'com.google.gms:google-services:4.3.0'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'se.yverling.wearto'
        versionCode 271110300 // Target SDK, version, release number, multi-apk number
        versionName '1.1.1'

        targetSdkVersion 28
        minSdkVersion 23

        testInstrumentationRunner 'se.yverling.wearto.test.WearToTestRunner'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        all {
            buildConfigField 'String', 'API_BASE_URL', "\"https://todoist.com/API/v7/\""
            buildConfigField 'String', 'LICENCES_URL', "\"https://github.com/erikryverling/wearto/blob/master/README.md#license-and-acknowledgments\""
        }

        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [usesCleartextTraffic:"true"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [usesCleartextTraffic:"false"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext {
    daggerVersion = '2.16'
    lifecycleVersion = '2.0.0'
    roomVersion = '2.1.0'
    ankoVersion = '0.10.2'
    stethoVersion = '1.5.0'
    androidTestVersion = '1.2.0'
    espressoVersion = '3.2.0'
}

dependencies {

    // -- Android dependencies

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.41'

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    implementation 'com.google.android.material:material:1.0.0'

    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    implementation 'com.google.android.gms:play-services-wearable:17.0.0'

    implementation 'com.google.firebase:firebase-core:17.0.1'

    // -- Third party dependencies

    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    implementation 'uk.co.samuelwall:material-tap-target-prompt:2.5.0'


    // -- Test dependencies

    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.16'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:2.22.0"
    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'

    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    androidTestImplementation 'org.mockito:mockito-android:2.22.0'

    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    //noinspection GradleDependency
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'
}